plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    def groovyVersion = "[4.0.4, )"
    def tablesawVersion = "[0.43.1, )"
    implementation "org.apache.groovy:groovy:${groovyVersion}"
    implementation "org.apache.groovy:groovy-sql:${groovyVersion}"
    implementation "tech.tablesaw:tablesaw-core:${tablesawVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation "org.slf4j:slf4j-simple:1.7.36"
}

group = 'se.alipsa.groovy'
version = '1.0.3-SNAPSHOT'
description = 'Java FX GUI for Groovy based data science analysis and applications'
java.sourceCompatibility = JavaVersion.VERSION_17

test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true

    useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(javadocJar)
            artifact(sourcesJar)
            pom {
                name = 'Groovy Data Utils'
                description = "${project.description}"
                url = "https://github.com/perNyfelt/data-utils"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/perNyfelt/data-utils/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/perNyfelt/data-utils/tree/master'
                    connection = 'scm:git:https://github.com/perNyfelt/data-utils.git'
                    developerConnection = 'scm:git:https://github.com/perNyfelt/data-utils.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
