plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
    id "com.github.ben-manes.versions" version "0.44.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    def groovyVersion = "[4.0.6, )"
    def tablesawVersion = "[0.43.1, )"
    // By using compileOnly instead of implementation, the jar will not be available at runtime
    // and hence users can use whatever compatible version of Groovy that they want
    compileOnly "org.apache.groovy:groovy:${groovyVersion}"
    compileOnly "org.apache.groovy:groovy-sql:${groovyVersion}"
    implementation("tech.tablesaw:tablesaw-core:${tablesawVersion}") {
        constraints {
            implementation "io.github.classgraph:classgraph:4.8.149"
            because "Fix CWE-611: Improper Restriction of XML External Entity Reference ('XXE')"
        }
        constraints {
            implementation "com.google.guava:guava:31.1-jre"
            because "Fix CWE-379: Creation of Temporary File in Directory with Incorrect Permissions"
        }
    }

    // for XlsxWriter, TODO: remove once contributed to tablesaw
    implementation 'org.apache.poi:poi-ooxml:5.2.3'
    // for OdsReader and Writer, TODO: remove once contributed to tablesaw
    implementation 'com.github.miachm.sods:SODS:1.5.2'
    // for XmlReader and writer, TODO: remove once contributed to tablesaw
    implementation 'org.dom4j:dom4j:2.1.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation "org.slf4j:slf4j-simple:2.0.5"
}

group = 'se.alipsa.groovy'
version = '1.0.3'
description = 'Java FX GUI for Groovy based data science analysis and applications'
java.sourceCompatibility = JavaVersion.VERSION_11

test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true

    useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(javadocJar)
            artifact(sourcesJar)
            pom {
                name = 'Groovy Data Utils'
                description = "${project.description}"
                url = "https://github.com/perNyfelt/data-utils"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/perNyfelt/data-utils/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/perNyfelt/data-utils/tree/master'
                    connection = 'scm:git:https://github.com/perNyfelt/data-utils.git'
                    developerConnection = 'scm:git:https://github.com/perNyfelt/data-utils.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        project.logger.lifecycle("Signing artifacts...")
        sign publishing.publications.maven
    } else {
        project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
    }
}
